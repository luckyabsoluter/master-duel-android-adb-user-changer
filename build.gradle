plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'org.beryx.jlink' version '3.2.1'
    id 'com.github.gmazzo.buildconfig' version '6.0.7'
    id 'org.graalvm.buildtools.native' version '0.11.1'
}

group = 'opensource.master_duel_android_adb_user_changer'
version = '1.1.0'

def javaVersion = 25
def javafxVersion = String.valueOf(javaVersion)

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

repositories {
    mavenCentral()
}

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

application {
    mainModule = 'opensource.master_duel_android_adb_user_changer'
    mainClass = 'opensource.master_duel_android_adb_user_changer.Launcher'
}

graalvmNative {
    binaries {
        main {
            imageName = 'master-duel-android-adb-user-changer'
            mainClass = 'opensource.master_duel_android_adb_user_changer.Launcher'
        }
    }
}

configurations {
    javafxWin
    javafxLinux
    javafxMac
}

dependencies {
    [
            javafxWin  : 'win',
            javafxLinux: 'linux',
            javafxMac  : 'mac'
    ].each { configName, platform ->
        ['base', 'graphics', 'controls', 'fxml'].each { module ->
            add(configName, "org.openjfx:javafx-${module}:${javafxVersion}:${platform}") {
                transitive = false
            }
        }
    }
}

tasks.named('shadowJar') {
    archiveClassifier.set('all')
    manifest {
        attributes(
                'Main-Class': 'opensource.master_duel_android_adb_user_changer.Launcher',
                'Implementation-Version': project.version.toString()
        )
    }
    mergeServiceFiles()
}

tasks.register('universalShadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    group = 'build'
    description = 'Builds a universal fat jar containing JavaFX native libraries for win/linux/mac.'

    archiveClassifier.set('all-universal')
    from sourceSets.main.output
    configurations = [
            project.configurations.runtimeClasspath,
            project.configurations.javafxWin,
            project.configurations.javafxLinux,
            project.configurations.javafxMac
    ]
    manifest {
        attributes(
                'Main-Class': 'opensource.master_duel_android_adb_user_changer.Launcher',
                'Implementation-Version': project.version.toString()
        )
    }
    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 true
}

buildConfig {
    packageName('opensource.master_duel_android_adb_user_changer')
    className('BuildConfig')
    useJavaOutput()
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
}

jlink {
    moduleName = 'opensource.master_duel_android_adb_user_changer'
    imageName = 'master-duel-android-adb-user-changer'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'master-duel-android-adb-user-changer'
        jvmArgs = ['--enable-native-access=javafx.graphics']
    }

    jpackage {
        imageName = 'master-duel-android-adb-user-changer'
        installerName = 'master-duel-android-adb-user-changer'
        installerType = 'app-image'
        appVersion = project.version.toString()
        if (System.getProperty('os.name').toLowerCase().contains('mac')) {
            imageOptions = ['--mac-package-identifier', 'opensource.master.duel.android.adb.user.changer']
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = javaVersion
}

tasks.named('run', JavaExec).configure {
    if (project.hasProperty('nativeMetadataAutoExitSeconds')) {
        jvmArgs("-Dnative.metadata.autoExitSeconds=${project.property('nativeMetadataAutoExitSeconds')}")
    }
    if (project.hasProperty('nativeMetadataTabSweep')) {
        jvmArgs("-Dnative.metadata.tabSweep=${project.property('nativeMetadataTabSweep')}")
    }
    if (project.hasProperty('nativeMetadataTabSweepIntervalMillis')) {
        jvmArgs("-Dnative.metadata.tabSweepIntervalMillis=${project.property('nativeMetadataTabSweepIntervalMillis')}")
    }
}

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def nativeCompileOutputDir = layout.buildDirectory.dir('native/nativeCompile')
def javafxWindowsNativeDlls = [
        'decora_sse.dll',
        'glass.dll',
        'javafx_font.dll',
        'javafx_iio.dll',
        'prism_common.dll',
        'prism_d3d.dll',
        'prism_sw.dll'
]

tasks.register('copyJavafxWindowsNativeDlls', Copy) {
    onlyIf { isWindows }
    dependsOn tasks.named('nativeCompile')
    into(nativeCompileOutputDir)
    from({
        configurations.runtimeClasspath.files
                .findAll { it.name.startsWith('javafx-graphics-') && it.name.endsWith('-win.jar') }
                .collect { zipTree(it) }
    }) {
        javafxWindowsNativeDlls.each { dllName ->
            include dllName
        }
    }
}

tasks.named('nativeCompile') {
    finalizedBy(tasks.named('copyJavafxWindowsNativeDlls'))
}
