plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'org.beryx.jlink' version '3.2.1'
    id 'com.github.gmazzo.buildconfig' version '6.0.7'
    id 'com.gluonhq.gluonfx-gradle-plugin' version '1.0.28'
}

group = 'opensource.master_duel_android_adb_user_changer'
version = '1.0.3'

def javaVersion = 21
def javafxVersion = String.valueOf(javaVersion)

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

repositories {
    mavenCentral()
}

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

application {
    mainModule = 'opensource.master_duel_android_adb_user_changer'
    mainClass = 'opensource.master_duel_android_adb_user_changer.Launcher'
}

gluonfx {
    target = 'host'
    // Include Prism software pipeline so runtime can fall back when ES2 init fails.
    enableSwRendering = true
    def graalHome = System.getenv('GRAALVM_HOME')
    if (graalHome == null || graalHome.trim().isEmpty()) {
        try {
            def process = ['mise', 'where', 'github:gluonhq/graal@gluon-23+25.1-dev-2409082136'].execute(null, project.rootDir)
            process.waitFor()
            if (process.exitValue() == 0) {
                graalHome = process.in.text.trim()
            }
        } catch (Exception ignored) {
            // fallback below
        }
    }
    if (graalHome == null || graalHome.trim().isEmpty()) {
        graalHome = System.getenv('JAVA_HOME')
    }
    if (graalHome != null) {
        graalvmHome = graalHome.trim()
    }
}

def ensureLinuxGraalStaticLibCompatibility = {
    if (!org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        return
    }

    def graalHome = project.extensions.findByName('gluonfx')?.graalvmHome
    if (graalHome == null || graalHome.toString().trim().isEmpty()) {
        graalHome = System.getenv('GRAALVM_HOME')
    }
    if (graalHome == null || graalHome.toString().trim().isEmpty()) {
        graalHome = System.getenv('JAVA_HOME')
    }
    if (graalHome == null || graalHome.toString().trim().isEmpty()) {
        return
    }

    def clibRoot = file("${graalHome.toString().trim()}/lib/svm/clibraries/linux-amd64")
    def glibcDir = new File(clibRoot, "glibc")
    if (!glibcDir.isDirectory()) {
        return
    }

    ['libjvm.a', 'liblibchelper.a'].each { libName ->
        def source = new File(glibcDir, libName)
        def target = new File(clibRoot, libName)
        if (!target.exists() && source.isFile()) {
            logger.lifecycle("Preparing Graal static library compatibility: ${target}")
            try {
                def relativeSource = target.parentFile.toPath().relativize(source.toPath())
                java.nio.file.Files.createSymbolicLink(target.toPath(), relativeSource)
            } catch (UnsupportedOperationException | IOException ignored) {
                java.nio.file.Files.copy(
                        source.toPath(),
                        target.toPath(),
                        java.nio.file.StandardCopyOption.REPLACE_EXISTING
                )
            }
        }
    }
}

tasks.matching { it.name == 'nativeLink' }.configureEach {
    doFirst {
        ensureLinuxGraalStaticLibCompatibility()
    }
}

configurations {
    javafxWin
    javafxLinux
    javafxMac
}

dependencies {
    [
            javafxWin  : 'win',
            javafxLinux: 'linux',
            javafxMac  : 'mac'
    ].each { configName, platform ->
        ['base', 'graphics', 'controls', 'fxml'].each { module ->
            add(configName, "org.openjfx:javafx-${module}:${javafxVersion}:${platform}") {
                transitive = false
            }
        }
    }
}

tasks.named('shadowJar') {
    archiveClassifier.set('all')
    manifest {
        attributes(
                'Main-Class': 'opensource.master_duel_android_adb_user_changer.Launcher',
                'Implementation-Version': project.version.toString()
        )
    }
    mergeServiceFiles()
}

tasks.register('universalShadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    group = 'build'
    description = 'Builds a universal fat jar containing JavaFX native libraries for win/linux/mac.'

    archiveClassifier.set('all-universal')
    from sourceSets.main.output
    configurations = [
            project.configurations.runtimeClasspath,
            project.configurations.javafxWin,
            project.configurations.javafxLinux,
            project.configurations.javafxMac
    ]
    manifest {
        attributes(
                'Main-Class': 'opensource.master_duel_android_adb_user_changer.Launcher',
                'Implementation-Version': project.version.toString()
        )
    }
    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 true
}

buildConfig {
    packageName('opensource.master_duel_android_adb_user_changer')
    className('BuildConfig')
    useJavaOutput()
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
}

jlink {
    moduleName = 'opensource.master_duel_android_adb_user_changer'
    imageName = 'master-duel-android-adb-user-changer'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'master-duel-android-adb-user-changer'
        jvmArgs = ['--enable-native-access=javafx.graphics']
    }

    jpackage {
        imageName = 'master-duel-android-adb-user-changer'
        installerName = 'master-duel-android-adb-user-changer'
        installerType = 'app-image'
        appVersion = project.version.toString()
        if (System.getProperty('os.name').toLowerCase().contains('mac')) {
            imageOptions = ['--mac-package-identifier', 'opensource.master.duel.android.adb.user.changer']
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = javaVersion
}
