plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'opensource.master_duel_android_adb_user_changer'
version = '1.0.1'

def javaVersion = 25
def javafxVersion = String.valueOf(javaVersion)

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

repositories {
    mavenCentral()
}

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

application {
    mainClass = 'opensource.master_duel_android_adb_user_changer.Launcher'
}

configurations {
    javafxWin
    javafxLinux
    javafxMac
}

dependencies {
    [
            javafxWin  : 'win',
            javafxLinux: 'linux',
            javafxMac  : 'mac'
    ].each { configName, platform ->
        ['base', 'graphics', 'controls', 'fxml'].each { module ->
            add(configName, "org.openjfx:javafx-${module}:${javafxVersion}:${platform}") {
                transitive = false
            }
        }
    }
}

tasks.named('shadowJar') {
    archiveClassifier.set('all')
    manifest {
        attributes 'Main-Class': 'opensource.master_duel_android_adb_user_changer.Launcher'
    }
    mergeServiceFiles()
}

tasks.register('universalShadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    group = 'build'
    description = 'Builds a universal fat jar containing JavaFX native libraries for win/linux/mac.'

    archiveClassifier.set('all-universal')
    from sourceSets.main.output
    configurations = [
            project.configurations.runtimeClasspath,
            project.configurations.javafxWin,
            project.configurations.javafxLinux,
            project.configurations.javafxMac
    ]
    manifest {
        attributes 'Main-Class': 'opensource.master_duel_android_adb_user_changer.Launcher'
    }
    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = javaVersion
}
